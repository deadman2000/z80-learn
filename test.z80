    SLDOPT COMMENT WPMEM, LOGPOINT, ASSERTION
    DEVICE ZXSPECTRUM128

ROM_OPEN_CHANNEL        EQU 0x1601
ROM_PRINT               EQU 0x203C
AT                      EQU 0x16
ScreenStart             EQU 0x4000


stack_top               EQU 0xFFF0
Code_Start              EQU 0x8000

    org Code_Start
    di
    ld sp, stack_top
    ei

    ; call DrawLine

Loop:
    halt

    ld a, 2
    call ROM_OPEN_CHANNEL

    ld a, 0
    ld d, %00000111
    call DrawBorderLine

    call PrintFrames ; 10372

    ld a, 1
    ld d, %00000111
    call DrawBorderLine

    call PrintLastKey ; 19301

    ld a, 2
    ld d, %00000111
    call DrawBorderLine

    call PrintMouseCoords ; 10374

    ld a, 3
    ld d, %00000111
    call DrawBorderLine

    call DrawCursor

    jr Loop

; Drawing vertical line from top to bottom screen
DrawLine:
    ld a, 0x80  ; draw first pixel in cell row
    ld hl, ScreenStart ; start cell address
    ld b, 3     ; blocks count
DrawBlock:
    push bc
    push hl
    ld b, 8     ; rows count
DrawRow:
    push bc
    push hl
    ld b, 8     ; cell height
DrawPixelCell:
    ld (hl), a
    inc h
    djnz DrawPixelCell
    ; Next row
    pop hl
    ld bc, 0x0020 ; next row offset
    add hl, bc
    pop bc
    djnz DrawRow
    ; Next block
    pop hl
    ld bc, 0x0800 ; next block offset
    add hl, bc
    pop bc
    djnz DrawBlock

    ret

PrintFrames:
    ; Get frame counter
    ld hl, (0x5C78)
    ld a, h
    call NumToHex
    ld (CoordsStr+3), de
    ld a, l
    call NumToHex
    ld (CoordsStr+6), de

    ; Text coords
    ld de, #0001
    ld (CoordsStr+1), de

    ; Printing
    ld de, CoordsStr
    ld bc, CoordsStrLen
    call ROM_PRINT
    ret

PrintLastKey:
    ld a, (0x5C08) ; Get last key
    ld (LastKeyCodeStr+13), a

    ; Printing
    ld de, LastKeyCodeStr
    ld bc, LastKeyCodeStrLen
    call ROM_PRINT
    ret

PrintMouseCoords:
    ld bc, 0xfbdf ; get mouse X
    in a, (c)
    call NumToHex
    ld (CoordsStr+3), de

    ld b, 0xff ; get mouse Y
    in a, (c)
    call NumToHex
    ld (CoordsStr+6), de

    ; Text coords
    ld de, #0000
    ld (CoordsStr+1), de

    ; Printing
    ld de, CoordsStr
    ld bc, CoordsStrLen
    call ROM_PRINT
    ret

CoordsStr:
    db AT,0,0,"00:00"
CoordsStrLen:  EQU $ - CoordsStr

LastKeyCodeStr:
    db AT,2,0,"Last key: x"
LastKeyCodeStrLen:  EQU $ - LastKeyCodeStr

; a - input number
; de - output chars
NumToHex:
    push af ; save a
    and 0x0f
    call HexChar
    ld d, a ; save result
    pop af  ; return original a value
    .4 rrca    ; Shift value to get high oct
    and 0x0f
    call HexChar
    ld e, a ; save result
    ret

; Convert a lower 4bit to hex letter
HexChar:
    cp 10
    jr c, Hex0 ; a < 10 ? return digit
    add 'A'-10
    ret
Hex0:
    add '0'
    ret

; Drawing line on border
; a line color
; d background color
DrawBorderLine:
    ld c, 254
    out (c), a
    ld b, 5
Delay1:
    djnz Delay1
    out (c), d
    ret

DrawCursor:
    ; calculating coords
    ; cell addr = 0x4000 + bn*0x0800 + ln*0x20  +  rn*0x100 + cx
    ;           = 0x4000 + bn << 11  + ln << 5  +  rn << 8  + cx
    ;           = 0x4000 + (cy & 0x18) << 8  + (cy & 7) << 5  +  rn << 8  + cx
    ; cy = y / 8 = y >> 3
    ; bn = cy / 8 = cy >> 3
    ; ln = cy % 8 = cy & 7
    ; rn = y % 8 = y & 7
    ; cx = x / 8
    ld bc, ScreenStart ; bc - cell addr
    ld a, (CursorY)
    .3 rra   ; a = cy = y / 8 = y >> 3
    ld e, a  ; e = cy
    and 0x18 ; a = cy & 0x18
    add b
    ld b, a  ; cell addr = 0x4000 + bn << 11
    ld a, e  ; a = cy
    and 7
    .5 rla   ; a = (cy & 7) << 5
    add a, c
    ld c, a
    jr nc, DrawCursor1
    inc b
DrawCursor1:
    ; bc = cell addr = 0x4000 + bn << 11 + ln << 5
    ; calc x
    ld a, (CursorX)  ; a = x
    .3 rra           ; a = cx = x / 8 = x >> 3
    add c
    ld c, a
    jr nc, DrawCursor2
    inc b
DrawCursor2:
    ; bc = cell addr = 0x4000 + bn << 11 + ln << 5 + cx

    ld de, bc
    ld b, 8
    ld hl, CursorImg
DrawCursorRow:
    ld a, (hl)
    ld (de), a
    inc d
    inc hl
    djnz DrawCursorRow
    ret

CursorX: db 16*8
CursorY: db 10*8
CursorImg:
    dg 1-------
    dg 11------
    dg 1-1-----
    dg 11-1----
    dg 111-1---
    dg 111111--
    dg 1-11----
    dg 11------

    SAVESNA "test.sna", Code_Start
